# 2.5.匿名结构

匿名结构可以用来组织数据，而不用显式地创建一个类型。下面的示例创建了一个结构，包含两个字段 `x` 和 `name`，并分别初始化它们的值为 `12` 和 “`foo`”：

```haxe
class Main { 
    static public function main() { 
        var myStructure = { x: 12, name: "foo"};
    } 
} 
```

一般语法规则如下：

- 一个包括在花括号 `{}` 内的结构
- 一个使用逗号 `,` 分隔的键值对列表
- 冒号 `:` 用来分隔的键和值，键名必须是一个有效的 [标识符（第5章）](http:///#)
- 值可以是任何 Haxe 表达式

规则 4 意味着结构可以被嵌套和混合，如：

```haxe
var user = {
    name : "Nicolas",
    age : 32, 
    pos : [//这是一个数组
        { x : 0, y : 0 }, //这是一个匿名结构
        { x : 1, y : -1 }//这也是一个匿名结构
    ],
}; 
```

匿名结构的字段像类一样可以通过点号操作符 `.` 访问，如下：

```haxe
// 获取键 name 的值，即 "Nicolas" 
user.name; 
// 设置 键 age 的值为 33 
user.age = 33; 
```

值得注意的是，使用匿名结构不会破坏类型系统。编译器确保只能访问可用的字段，也就是说，如下的程序不会编译：

```haxe
class Test { 
    static public function main() {
        var point = { x: 0.0, y: 12.0 }; 
        // { y : Float, x : Float } 没有字段 z 
        point.z; 
    } 
}
```

错误信息表明编译器了解 `point` 的类型：它是一个包含了两个 `Float` 类型 `x` 和 `y` 字段的匿名结构。由于它没有字段 `z`，访问失败。 `point` 的类型通过 [类型推断（第3.6节）](http:///#) 被得知，这使得我们不需要为局部变量使用显式的类型声明。但是，如果 `point` 是一个字段，那么它必须通过如下形式进行声明：

```haxe
class Path { 
    var start : { x : Int, y : Int };
    var target : { x : Int, y : Int };
    var current : { x : Int, y : Int }; 
} 
```

为了避免这种冗余的类型声明形式，尤其是对于更复杂的结构，建议使用一个 [typedef（第3.1节）](http:///#) 关键字：

```haxe
typedef Point = { x : Int, y : Int } 

class Path { 
    var start : Point; 
    var target : Point; 
    var current : Point; 
}
```



# 2.5.1.结构值的JSON形式

还可以为匿名结构使用 **JavaScript 对象记法**，为键使用使用字符串字面值：

```haxe
var point = { "x" : 1, "y" : -5 };
```

虽然允许任意的字符串字面值，但如必须是有效的 [Haxe 标识符（第5章）](http:///#)，字段才会被认为是类型的一部分。否则，Haxe 的语法不会允许对该字段访问的表达式，并且必须通过使用 `Reflect.field` 和 `Reflect.setField` 来使用 [反射（10.7）](http:///#)。

It is also possible to use **JavaScript Object Notation** for structures by using **string literals** for the keys:

```haxe
var point = { "x" : 1, "y" : -5 };
```

While any string literal is allowed, the field is only considered part of the type if it is a valid [Haxe identifier](https://haxe.org/manual/expression.html#define-identifier). Otherwise, Haxe syntax does not allow expressing access to such a field, and [reflection](https://haxe.org/manual/std-reflection.html) has to be employed through the use of `Reflect.field` and `Reflect.setField` instead.



# 2.5.2. 结构类型的类记法

如果想要定义一个 **结构类型** 时，Haxe 允许使用在 [类字段（第4章）](http:///#) 中所使用的语法来定义结构类型。下面的 [typedef（第3.1节）](http:///#) 声明了一个  `Point` 类型，它包含了两个 `Int` 类型的**变量字段** `x` 和 `y`：

When defining a structure type, Haxe allows the use of the same syntax described in [Class Fields](https://haxe.org/manual/class-field.html). The following [typedef](https://haxe.org/manual/type-system-typedef.html) declares a `Point` type with variable fields `x` and `y` of type `Int`:

```haxe
typedef Point = {
    var x : Int; 
    var y : Int;
}
```

##### [since Haxe 4.0.0](https://haxe.org/manual/types-structure-class-notation.html#since-haxe-4.0.0)

The fields of a structure may also be declared with `final`, which only allows them to be assigned once. Such a structure will only [unify](https://haxe.org/manual/type-system-unification.html) with other types if the corresponding fields are also `final`.

结构的字段也可以被修饰为 `final` ，这意味着这些字段只能被赋值一次。同时这类结构只能被统一为对应字段同样修饰为 `final` 的类型。



# 2.5.3 Optional Fields

结构类型的字段可以被修饰为可选的。在标准形式的声明下，通过位于字段名称前放置一个问号 `?` 进行修饰。

Fields of a structure type can be made optional. In the standard notation, this is achieved by prefixing the field name with a question mark `?`:

```haxe
typedef User = {
  age : Int,
  name : String,
  ?phoneNumber : String
}
```

在类记法（Class Notation) 形式的声明下，则可以使用 `@:optional` 元数据来修饰可选字段

In class notation, the `@:optional` metadata can be used instead:

```haxe
typedef User = {
  var age : Int;
  var name : String;
  @:optional var phoneNumber : String;
}
```

从 Haxe 4.0 开始，允许在类记法的结构声明中使用问号 `?` 修饰可选字段：

```haxe
typedef User = {
  var age : Int;
  var name : String;
  var ?phoneNumber : String;
}
```



# 2.5.4.性能影响

通过扩展、[结构子类型化（第3.5.2）](http:///#) 使用匿名结构，在编译为 [动态类型目标语言（第2.2节）](http:///#) 时不会对性能产生影响。然而，对于 [静态类型目标语言（第2.2节）](http:///#) 来说，必须执行一个动态的查找，这通常会慢于对静态字段的访问。



# 2.5.5.扩展

扩展被用于表示该结构具有某个给定类型的所有字段外，还具有一些额外的字段：

Extensions are used to express that a structure has all the fields of a given type as well as some additional fields of its own:

```haxe
typedef IterableWithLength<T> = {
  > Iterable<T>,
  // read only property
  var length(default, null):Int;
}

class Main {
  static public function main() {
    var array = [1, 2, 3];
    var t:IterableWithLength<Int> = array;
  }
}
```

大于操作符 `>` 被用于表示创建一个 `Iterable<T>` 的扩展。然后补充额外的类字段，此例中，需要一个 `Int` 类型的 只读 属性 `length` 。

The greater-than operator `>` denotes that an extension of `Iterable<T>` is being created, with the additional class fields following. In this case, a read-only [property](https://haxe.org/manual/class-field-property.html) `length` of type `Int` is required.

为了能与 `IterableWithLength<T>` 兼容，一个类必须能够与 `Iterable<T>` 兼容的同时提供一个 只读 的 `Int` 类型属性 `length`。以上例子中赋值了一个 `Array`，其刚好满足这两点要求。

In order to be compatible with `IterableWithLength<T>`, a type must be compatible with `Iterable<T>` and provide a read-only `length` property of type `Int`. The previous example assigns an `Array`, which happens to fulfill these requirements.



##### [since Haxe 3.1.0](https://haxe.org/manual/types-structure-extensions.html#since-haxe-3.1.0)

多个结构可以被同时扩展：

Multiple structures can be extended at once:

```haxe
typedef WithLength = {
  var length(default, null):Int;
}

typedef IterableWithLengthAndPush<T> = {
  > Iterable<T>,
  > WithLength,
  function push(a:T):Int;
}

class Main {
  static public function main() {
    var array = [1, 2, 3];
    var t:IterableWithLengthAndPush<Int> = array;
  }
}
```



##### [since Haxe 4.0.0](https://haxe.org/manual/types-structure-extensions.html#since-haxe-4.0.0)

有一个额外的记法可以被用于扩展，通过以 `&` 符号分格每一个欲扩展的结构来表示。

An alternative notation for extension can be used, denoted by separating each extended structure with an `&` symbol.

```haxe
typedef Point2D = {
  var x:Int;
  var y:Int;
}

typedef Point3D = Point2D & {z:Int};

class Main {
  static public function main() {
    var point:Point3D = {x: 5, y: 3, z: 1};
  }
}
```

补充另一例子：

```haxe
typedef IterableWithLengthAndPush<T> = {
  > Iterable<T>,
  > WithLength,
  function push(a:T):Int;
}
//可以改写为：
typedef IterableWithLengthAndPush<T> =
Iterable<T> & WithLength &
{
    function push(a:T):Int;
}
```

