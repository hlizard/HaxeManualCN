# 2.4.枚举实例

Haxe 提供了强大的枚举类型（简写：enum），它实际上是一个代数数据类型（ADT）。虽然它们不能有任何表达式，但用来描述数据结构是非常实用的：

```haxe
enum Color {
    Red;    
    Green;
    Blue;
    Rgb(r:Int, g:Int, b:Int);
} 
```

从语义上看，这个 enum 描述了一个颜色，可能是红色，绿色，蓝色或者一个特定的 RGB 值。它的语法构成如下：

- 关键字 `enum` 表示我们要声明一个枚举
- `Color` 是枚举类型的名称，可以是任何符合类型标识符规则的字符
- 闭合的花括号中间是枚举的构造函数
- `Red`，`Green` 和 `Blue` 不需要参数
- 而 `Rgb` 需要三个 Int 类型的参数，分别是 `r`，`g` 和 `b`

Haxe 类型系统提供了一个可以和所有的枚举类型统一的类型：

> **定义** `Enum<T>`：
>
> 这个类型可以与所有的枚举类型兼容。在编译时，`Enum<T>` 可以被看作枚举类型的通用基本类型。然而，这个关系不会反应在生成的代码中。



# 2.4.1.Enum构造函数

类似于类和它们的构造函数，枚举类型也可以通过构造函数实例化。然而，与类不同，枚举提供了多个构造函数，可以简单地通过它们的名字进行调用：

```haxe
var a = Red;
var b = Green; 
var c = Rgb(255, 255, 0)
```

在这段代码中变量 `a`，`b` 和 `c` 的类型为 `Color`。变量 `c` 使用 `Rgb` 构造函数和参数来初始化。

所有枚举实例都可以被赋值到一个名为 `EnumValue` 的特殊类型上：

> **定义** EnumValue：
>
> `EnumValue` 是一个可以与任意 enum 实例统一的特殊类型。它被 用于 Haxe 标准库，来提供对任意枚举实例的某些操作。它也可以用于用户代码中，比如当某个 API 需要一个非特定类型的枚举实例时可以使用它。

需要着重区分枚举类型和枚举构造函数，如这个示例所展示的：

```haxe
enum Color
{
    Red;
    Green;
    Blue;
    Rgb(r:Int,g:Int,b:Int);
}

class Main{
    public static function main(){
        var ec:EnumValue = Red; //有效
        var en:Enum<Color> = Color; //有效
            
        // error, Color 应该是 Enum<Color>
        // var x :Enum<Color> = Red;
    }
}
```

如果上面注释掉的行没有被注释，那么程序不会通过编译，因为 `Red` （一个枚举构造函数）不能被赋值到一个 `Enum<Color>` （一个枚举类型）类型的变量。这个关系就像类和类的实例。

> 花絮 具象化 `Enum<T>` 类型参数：
>
> 手册的一个读者困惑于上面示例中 `Color` 和 `Enum<Color>` 的区别。实际上在这里使用一个具体的类型参数是没意义的，只是为了演示目的。通常我们会在这里省略具体的类型声明，并让 [类型推断（第3.6节）](/3.类型系统/3.6.类型推断) 来处理它。
>
> 然而，推断出的类型会不同于 `Enum<Color>` 。编译器会推断为一个含有枚举类型中的构造函数作为其 “字段” 的伪类型。自 Haxe3.2.0 起，不能在语法中表达这个类型，也没有必要这样做。



# 2.4.2.使用枚举

当需要用到一组值的有限集时，枚举类型是一个不错的选择。单个的构造函数表示允许的变型，并使编译器检查是否被所有可能的值所遵循：

```haxe
enum Color{
    Red;
    Green;
    Blue;
    Rgb(r:Int, g:Int, b:Int);
}
class Main{
    public static function main(){
        var color = getColor();
        switch(color){
            case Red:
                trace("color was red");
            case Green:
                trace("color was green");
            case Blue:
                trace("color was blue");
            case Rgb(r,g,b):
                trace("color had a red value of " + r);
        }
    }
    
    static function getColor():Color{
        return Rgb(255, 0, 255);
    }
}
```

在取得 `getColor()` 的返回值赋值到 `color` 上的值之后， [switch 语句（第5.17节）](/5.表达式/5.17.switch) 根据 `color` 的值进行分支。前三个分支 `Red`，`Green` 和 `Blue` 此刻无关紧要，他们分别对应 `Color` 中不带参数的那几个构造函数。最后的 `Rgb(r,g,b)` 展示了如何提取构造函数中的参数：它们可以作为局部变量在 `case` 表达式体中使用，就像使用 [var 表达式（第5.10节）](/5.表达式/5.10.var) 一样。关于进阶使用 `switch` 语句的信息将在 [模式匹配（第6.4节）](/6.语言特性/6.4.模式匹配) 章节进一步展开。